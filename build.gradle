group 'net.swiftzer.semver'
version '1.0.0'

buildscript {
    ext.kotlin_version = '1.1.51'
    ext.dokka_version = '0.9.15'

    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
        classpath 'com.novoda:bintray-release:0.5.0'
    }
}

apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'com.novoda.bintray-release'

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    testCompile 'junit:junit:4.12'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.6"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.6"
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc"
    linkMapping {
        dir = "src/main/java"
        url = "https://github.com/swiftzer/semver/tree/master/src/main/java"
        suffix = "#L"
    }
}

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = 'javadoc'
    outputDirectory = javadoc.destinationDir
    inputs.dir 'src/main/java'
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publish {
    userOrg = 'swiftzer'
    groupId = 'net.swiftzer.semver'
    artifactId = 'semver'
    publishVersion = '1.0.0'
    desc = 'Kotlin data class for Semantic Versioning 2.0.0 specification (SemVer)'
    website = 'https://github.com/swiftzer/semver'
}

